import io.github.fourlastor.construo.Target
import org.gradle.internal.os.OperatingSystem

plugins {
	id "dev.prozilla.pine.conventions"
	id "dev.prozilla.pine.examples.conventions"
	id "com.gradleup.shadow" version "8.3.9"
	id "io.github.fourlastor.construo" version "1.7.1"
}

application {
	mainClass = "dev.prozilla.pine.examples.flappybird.FlappyBird"
}

dependencies {
	implementation project(":pine-editor")
}

tasks.register("start", JavaExec) {
	mainClass = "dev.prozilla.pine.examples.flappybird.FlappyBird"
	classpath = sourceSets.main.runtimeClasspath
    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        jvmArgs "-XstartOnFirstThread"
    }
	systemProperty "dev-mode", "true"
}

tasks.register("editor", JavaExec) {
	mainClass = "dev.prozilla.pine.examples.flappybird.Editor"
	classpath = sourceSets.main.runtimeClasspath
}

construo {
	name.set("flappy-bird")
	humanName.set("Flappy Bird")
	// Optional, an alternative jar task name to base the build upon
//	jarTask.set("myJarTaskName")
	targets.configure {
		create("win", Target.Windows) {
			architecture.set(Target.Architecture.X86_64)
			jdkUrl.set("https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.7%2B6/OpenJDK21U-jdk_x64_windows_hotspot_21.0.7_6.zip")
		}
		create("mac", Target.MacOs) {
			architecture.set(Target.Architecture.AARCH64)
			jdkUrl.set("https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.7%2B6/OpenJDK21U-jdk_aarch64_mac_hotspot_21.0.7_6.tar.gz")
			// macOS needs an identifier
			identifier.set(application.mainClass)
			// Optional: icon for macOS
//			macIcon.set(project.file("path/to/mac-icon.icns"))
		}
		create("linux", Target.Linux) {
			architecture.set(Target.Architecture.X86_64)
			jdkUrl.set("https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.7%2B6/OpenJDK21U-jdk_x64_linux_hotspot_21.0.7_6.tar.gz")
		}
	}
}